#############################	CLASS MARKERS

LEXICON ClassMarker
б:+в+[sg,m]
б:+й+[sg,f]
б:+б+[sg,n]
б:+р+[pl,an]
б:+б+[pl,inan]

ALIAS ClassMarker CM

#############################	MAIN CASES 4 ATTRIBUTIVE ARGUMENTS

PATTERN CaseEndAttr
CEACore CEAEnd

LEXICON CEACore
:щу[sg,m]
:лълъи[sg]
:лу[pl]

LEXICON CEAEnd
<erg>:ди
<gen>:б
#a shitton of work for twol
<dat>:й

#############################	LOCATIVE CASES

PATTERN LocEnd
CoreE EndE
CoreL

LEXICON CoreE
<sup>:у
<sup>:е
<cont>:чу
<apud>:хе
<apud>:хи
<apud>:хъи
<in>:лӏи
<in>:лълъу
<in>:лъи
<sub>:кьи

LEXICON EndE
<ess>:[ess]
<el>:ру[ess]
<trl>:ку[trl]

LEXICON CoreL
<sup><l>:а
<cont><l>:чу
<apud><l>:ха
<apud><l>:хъа
<in><l>:лӏа
<in><l>:лълъа
<in><l>:лъа
<sub><l>:кьа

PATTERNS
NumCore NumEnd
One CM|CaseEndAttr[sg]
#одни?
Big[i]|(Basic Big[d,celat]) LinkSuffix[celat]
Big[i]|(Basic Big[d,comp]) LinkSuffix[comp]
MultNumCore MultSuffix 

PATTERN NumCore
Big[i]
Basic > Big[d]

PATTERN NumEnd
DistSuffix
CardSuffix > CaseEndAttr[sg]
[:й] (OrdSuffix[an] CM[-inan]) | (OrdSuffix[inan] CM[-an])

PATTERN MultNumCore
One[card]
Basic[-excm]
Big[i]
Basic > Big[d]

LEXICON MultSuffix
<mult>:рцӏи

LEXICON DistSuffix
<dist>:ркьа

LEXICON CardSuffix
<card>:да

LEXICON OrdSuffix
<ord>:хо[inan]
<ord>:лъа[an]

LEXICON LinkSuffix
<comp>:лъи[comp]
<celat>:чӏуку[celat]

LEXICON One
це<num><card>:це[card]
це<num><ord>:гьечӏиссеъассу[ord]

LEXICON Basic
кӏе<num>:кӏе[excm]
гьабу<num>:гьабу
бугъу<num>:бугъу
ишту<num>:ишту
инлӏи<num>:инлӏи
гьакьу<num>:гьакьу
бикьи<num>:бикьи
гьачӏа<num>:гьачӏа

LEXICON Big
гьацӏа<num>:гьацӏа[i]
гьацӏа<num>:цӏали[d,comp]
#
бешуну<num>:бешуну[i]
бешуну<num>:бешун[d,celat]
#
азару<num>:азару[i]
азару<num>:азару[d,celat]

PATTERNS
Personal
Demonstrative
ReflRecip
Interrogative
Indefinite

#############################

PATTERN Personal
PersonalCore[-gen]
PersonalCore[gen,sg] CM[sg,m] (CaseEndAttr[m]|LocEnd)?
PersonalCore[gen,sg] CM[sg,-m] (CaseEndAttr[sg,-m]|LocEnd)?
PersonalCore[gen,-sg] CM[pl] (CaseEndAttr[-sg]|LocEnd)?
PersonalCore[dat] LocEnd

PATTERN Demonstrative
DemCore DemEnd
DemCore CaseEndAttr
DemCore LocEnd

PATTERN ReflRecip
PersonalCore ReflEnd
ReflCore[abs] CM ReflEnd: :ReflEnd?
ReflCore[obl] CEACore[sg]|[:ду] CEAEnd ReflEnd: :ReflEnd?

PATTERN Interrogative
IntCore[b] CM[-inan] (IntSuffix[an] [<q>:ла])?
IntCore[b] CM[-an] (IntSuffix[inan] [<q>:ла])?
IntCore[o] [<q>:ла]
IntCore[x] CM > IntSuffix > [<q>:ла]

PATTERN Indefinite
ReflCore[abs] CM[sg] :ReflCore[abs] :CM[sg] [<ind>:] CaseEndAttr[sg]?
IndCore ([:да] CaseEndAttr[pl])?
IntCore[|[b,x]] CM IndEnd
IntCore[o] IndEnd
[<pn>:] :One[card] CM[sg] [<indef>:дала]

############################# CM, CaseEndAttr/CEA, LocEnd @ ATFM

LEXICON PersonalCore
ден<pn><abs>:ден[sg]
ден<pn><erg>:ишкур[sg]
ден<pn><dat>:ди[dat,sg]
ден<pn><gen>:ди[gen,sg]
мин<pn><abs>:мин[sg]
мин<pn><erg>:мин[sg]
мин<pn><dat>:ду[dat,sg]
мин<pn><gen>:ди[gen,sg]
илӏи<pn><abs>:илӏи[pl]
илӏи<pn><erg>:илӏи[pl]
илӏи<pn><dat>:илӏи[dat,pl]
илӏи<pn><gen>:илӏи[gen,pl]
ищи<pn><abs>:ищи[pl]
ищи<pn><erg>:ищи[pl]
ищи<pn><dat>:ищи[dat,pl]
ищи<pn><gen>:ищи[gen,pl]
бишти<pn><abs>:бишти[pl]
бишти<pn><erg>:бишти[pl]
бишти<pn><dat>:бишти[dat,pl]
бишти<pn><gen>:бишти[gen,pl]

LEXICON DemCore
гьа<pn>:гьа
га<pn>:га
гьу<pn>:гьу
го<pn>:го
до<pn>:до

PATTERN DemEnd
[<spec>:шту]? CM
[<adv>:лълъа] EndE[trl]?
[<spec>:ла]

LEXICON ReflCore
гьи<pn>:гьи[abs]
ин<pn>:ин[obl]

LEXICON ReflEnd
<refl>:да

LEXICON IntCore
э<pn><abs>:эᵸ[b,abs]
э<pn><obl><an>:лълъе[b,obl]
э<pn><obl><inan>:лълъун[b,obl]
эпи<pn>:эпи[o]
ин<pn>:ин[o]
чаму<pn>:чаму[o]
инда<pn>:инда[o]
ину<pn>:ину[o]
ишту<pn>:ишту[x]

LEXICON IntSuffix
<inan>:ху[inan]
<an>:лъи[an]

LEXICON IndCore
тIоргу<pn>:тIоргу

LEXICON IndEnd
<indef>:кIвала
<indef>:хвала

