PATTERNS
NumCore NumEnd
One CM|CaseEndAttr[sg]
#одни?
Big[i] | (Basic Big[d,celat]) LinkSuffix[celat]
Big[i] | (Basic Big[d,comp]) LinkSuffix[comp]
MultNumCore MultSuffix 

PATTERN NumCore
Big[i]
Basic > Big[d]

PATTERN NumEnd
DistSuffix
CardSuffix > CaseEndAttr[sg]
[:й] (OrdSuffix[an] CM[-inan]) | (OrdSuffix[inan] CM[-an])

PATTERN MultNumCore
One[card]
Basic[-excm]
Big[i]
Basic > Big[d]

LEXICON MultSuffix
<mult>:рцӏи

LEXICON DistSuffix
<dist>:ркьа

LEXICON CaseEndAttr
<erg><I>:щуди[sg]
<erg><II>:лълъиди[sg]
<erg><III>:лълъиди[sg]
<erg><IV>:луди
<erg><V>:луди

<gen><I>:щуб[sg]
<gen><II>:лъилъи[sg]
<gen><III>:лъилъи[sg]
<gen><IV>:луб
<gen><V>:луб

<dat><I>:щуй[sg]
<dat><II>:лъий[sg]
<dat><III>:лълъий[sg]
<dat><IV>:луй
<dat><V>:луй

LEXICON ClassMarker
<I>:в[sg,m]
<II>:й[sg,f]
<III>:б[sg,n]
<IV>:р[pl,an]
<V>:б[pl,inan]

ALIAS ClassMarker CM

LEXICON CardSuffix
<card>:да

LEXICON OrdSuffix
<ord>:хо[inan]
<ord>:лъа[an]

LEXICON LinkSuffix
<comp>:лъи[comp]
<celat>:чӏуку[celat]

LEXICON One
це<num><card>:це[card]
це<num><ord>:гьечӏиссеъассу[ord]

LEXICON Basic
кӏе<num>:кӏе[excm]
гьабу<num>:гьабу
бугъу<num>:бугъу
ишту<num>:ишту
инлӏи<num>:инлӏи
гьакьу<num>:гьакьу
бикьи<num>:бикьи
гьачӏа<num>:гьачӏа

LEXICON Big
гьацӏа<num>:гьацӏа[i]
гьацӏа<num>:цӏали[d,comp]
бешуну<num>:бешуну[i]
бешуну<num>:бешун[d,celat]
азару<num>:азару[i]
азару<num>:азару[d,celat]
